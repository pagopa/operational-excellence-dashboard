let api_url = "/api/v1/services/1";
let api_hosts = datatable (name: string) [ "app-backend.io.italia.it", "api-app.io.pagopa.it" ];
let api_httpStatus = datatable (name: int) [ 200 , 304 ];
let success=AzureDiagnostics
| where originalHost_s in (api_hosts)
| where requestUri_s startswith api_url
| where httpStatus_d in (api_httpStatus)
| summarize count() by HTTPStatus=" Success", bin(TimeGenerated, 5m);
let fail=AzureDiagnostics
| where originalHost_s in (api_hosts)
| where requestUri_s startswith api_url
| where httpStatus_d !in (api_httpStatus)
| extend HTTPStatus = case(
httpStatus_d between (200 .. 299), "2XX",
httpStatus_d between (300 .. 399), "3XX",
httpStatus_d between (400 .. 499), "4XX",
"5XX"
)
| summarize count() by HTTPStatus, bin(TimeGenerated, 5m);
union success, fail
| render areachart
